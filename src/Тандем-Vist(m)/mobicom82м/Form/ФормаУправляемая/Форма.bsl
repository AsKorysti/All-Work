
&НаСервере
Функция ЗагрузитьНастройки_Сервер()
	оОбъект = РеквизитФормыВЗначение("Объект");
	оОбъект.НастройкиЗагрузить(ОбъектыВыгрузки, ОбъектыЗагрузки, ЭтаФорма.Заголовок);
	ЗначениеВРеквизитФормы(оОбъект,"Объект");
КонецФункции
&НаСервере
Функция СохранитьНастройки_Сервер()
	оОбъект = РеквизитФормыВЗначение("Объект");
	оОбъект.НастройкиСохранить(ОбъектыВыгрузки, ОбъектыЗагрузки);
	//ЗначениеВРеквизитФормы(оОбъект,"Объект");
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.НадписьИмяФайла.Заголовок = "Файл обработки: "+РеквизитФормыВЗначение("Объект").ИспользуемоеИмяФайла;
	ЗагрузитьНастройки_Сервер();
КонецПроцедуры

&НаСервере
Функция ТекущийПользователь_Сервер()
	Возврат ПараметрыСеанса.ТекущийПользователь;
КонецФункции
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ФТПИспользоватьПриИзменении("ПриОткрытии");
	ПроксиИспользоватьПриИзменении("ПриОткрытии");
	АвтообменИспользоватьПриИзменении("ПриОткрытии");
	
	Если ТекущийПользователь_Сервер() = Объект.АвтообменПользователь Тогда
		Если НЕ Объект.АвтообменИспользовать Тогда
			Объект.АвтообменИспользовать = Истина;
			АвтообменИспользоватьПриИзменении("ПриОткрытии");
		КонецЕсли;
		//Возврат;
		// запустить автообмен
		Автообмен();
		//ЭтаФорма.Закрыть();
		//ЗавершитьРаботуСистемы(Ложь);
		СохранитьНастройки_Сервер();
		ПрекратитьРаботуСистемы();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	СохранитьНастройки_Сервер();
КонецПроцедуры


&НаКлиенте
Функция ЭтоПустойСписок(сп)
	ПустоеЗначениеОтбора = "< Отбор не задан >";
	
	Если сп.Количество()=1 Тогда
		Если сп.Получить(0).Значение=ПустоеЗначениеОтбора Тогда
			Возврат Истина;
		КонецЕсли;
	ИначеЕсли сп.Количество()=0 Тогда
		Возврат Истина;
	КонецЕсли;
	Возврат Ложь;
КонецФункции

&НаКлиенте
Процедура ОбъектыВыгрузкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя="ОбъектыВыгрузкиОтбор" ИЛИ Поле.Имя="ОбъектыВыгрузкиИсключение" Тогда
		Если Элемент.ТекущиеДанные.ВидЗначения="" ИЛИ Лев(Элемент.ТекущиеДанные.ВидЗначения,1)="*" Тогда
			СтандартнаяОбработка = Ложь;
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ОбъектыВыгрузкиОтборНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если ЭтоПустойСписок(Элементы.ОбъектыВыгрузки.ТекущиеДанные.Отбор) Тогда
		Элементы.ОбъектыВыгрузки.ТекущиеДанные.Отбор.Очистить();
		Элементы.ОбъектыВыгрузки.ТекущиеДанные.Отбор.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка."+Элементы.ОбъектыВыгрузки.ТекущиеДанные.ВидЗначения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыВыгрузкиОтборПриИзменении(Элемент)
	ПустоеЗначениеОтбора = "< Отбор не задан >";
	
	Сч = Элементы.ОбъектыВыгрузки.ТекущиеДанные.Отбор.Количество();
	Пока Сч >0 Цикл
		Сч = Сч-1;
		Если НЕ ЗначениеЗаполнено(Элементы.ОбъектыВыгрузки.ТекущиеДанные.Отбор.Получить(Сч).Значение) Тогда
			Элементы.ОбъектыВыгрузки.ТекущиеДанные.Отбор.Удалить(Элементы.ОбъектыВыгрузки.ТекущиеДанные.Отбор.Получить(Сч));	
		КонецЕсли;
	КонецЦикла;
	
	Если Элементы.ОбъектыВыгрузки.ТекущиеДанные.Отбор.Количество()=0 Тогда
		Элементы.ОбъектыВыгрузки.ТекущиеДанные.Отбор.ТипЗначения = Новый ОписаниеТипов("Строка");
		Элементы.ОбъектыВыгрузки.ТекущиеДанные.Отбор.Добавить(ПустоеЗначениеОтбора);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыВыгрузкиОтборОчистка(Элемент, СтандартнаяОбработка)
	ПустоеЗначениеОтбора = "< Отбор не задан >";
	
	СтандартнаяОбработка=Ложь;
	Элементы.ОбъектыВыгрузки.ТекущиеДанные.Отбор.Очистить();
	Элементы.ОбъектыВыгрузки.ТекущиеДанные.Отбор.ТипЗначения = Новый ОписаниеТипов("Строка");
	Элементы.ОбъектыВыгрузки.ТекущиеДанные.Отбор.Добавить(ПустоеЗначениеОтбора);
КонецПроцедуры


&НаКлиенте
Процедура ОбъектыВыгрузкиИсключениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если ЭтоПустойСписок(Элементы.ОбъектыВыгрузки.ТекущиеДанные.Исключение) Тогда
		Элементы.ОбъектыВыгрузки.ТекущиеДанные.Исключение.Очистить();
		Элементы.ОбъектыВыгрузки.ТекущиеДанные.Исключение.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка."+Элементы.ОбъектыВыгрузки.ТекущиеДанные.ВидЗначения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыВыгрузкиИсключениеПриИзменении(Элемент)
	ПустоеЗначениеОтбора = "< Отбор не задан >";
	
	Сч = Элементы.ОбъектыВыгрузки.ТекущиеДанные.Исключение.Количество();
	Пока Сч >0 Цикл
		Сч = Сч-1;
		Если НЕ ЗначениеЗаполнено(Элементы.ОбъектыВыгрузки.ТекущиеДанные.Исключение.Получить(Сч).Значение) Тогда
			Элементы.ОбъектыВыгрузки.ТекущиеДанные.Исключение.Удалить(Элементы.ОбъектыВыгрузки.ТекущиеДанные.Исключение.Получить(Сч));	
		КонецЕсли;
	КонецЦикла;
	
	Если Элементы.ОбъектыВыгрузки.ТекущиеДанные.Исключение.Количество()=0 Тогда
		Элементы.ОбъектыВыгрузки.ТекущиеДанные.Исключение.ТипЗначения = Новый ОписаниеТипов("Строка");
		Элементы.ОбъектыВыгрузки.ТекущиеДанные.Исключение.Добавить(ПустоеЗначениеОтбора);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыВыгрузкиИсключениеОчистка(Элемент, СтандартнаяОбработка)
	ПустоеЗначениеОтбора = "< Отбор не задан >";
	
	СтандартнаяОбработка=Ложь;
	Элементы.ОбъектыВыгрузки.ТекущиеДанные.Исключение.Очистить();
	Элементы.ОбъектыВыгрузки.ТекущиеДанные.Исключение.ТипЗначения = Новый ОписаниеТипов("Строка");
	Элементы.ОбъектыВыгрузки.ТекущиеДанные.Исключение.Добавить(ПустоеЗначениеОтбора);
КонецПроцедуры


&НаКлиенте
Процедура ФТПИспользоватьПриИзменении(Элемент)
	Элементы.ФТПСервер.Доступность = Объект.ФТПИспользовать;
	Элементы.ФТППорт.Доступность = Объект.ФТПИспользовать;
	Элементы.ФТПТаймаут.Доступность = Объект.ФТПИспользовать;
	Элементы.ФТППользователь.Доступность = Объект.ФТПИспользовать;
	Элементы.ФТППароль.Доступность = Объект.ФТПИспользовать;
	Элементы.ФТППроверить.Доступность = Объект.ФТПИспользовать;
КонецПроцедуры

&НаКлиенте
Процедура ПроксиИспользоватьПриИзменении(Элемент)
	Элементы.ПроксиСервер.Доступность = Объект.ПроксиИспользовать;
	Элементы.ПроксиПорт.Доступность = Объект.ПроксиИспользовать;
	Элементы.ПроксиПользователь.Доступность = Объект.ПроксиИспользовать;
	Элементы.ПроксиПароль.Доступность = Объект.ПроксиИспользовать;
КонецПроцедуры


&НаКлиенте
Процедура КаталогВыбрать(Команда)
	лРежим	= РежимДиалогаВыбораФайла.ВыборКаталога;
	
	ДиалогОткрытияФайла	= Новый ДиалогВыбораФайла(лРежим);
	ДиалогОткрытияФайла.Каталог				= Объект.КаталогОбмена;
	ДиалогОткрытияФайла.МножественныйВыбор	= Ложь;
	ДиалогОткрытияФайла.Заголовок			= "Выберите путь к данным";
	
	Если НЕ ДиалогОткрытияФайла.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	Объект.КаталогОбмена	= ДиалогОткрытияФайла.Каталог;
	Если НЕ Прав(Объект.КаталогОбмена,1)="\" Тогда
		Объект.КаталогОбмена = Объект.КаталогОбмена+"\";
	КонецЕсли;
	ЗагрузитьНастройки_Сервер();
КонецПроцедуры

&НаКлиенте
Процедура АвтообменПользовательОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТекущийПользователь_Сервер() = ВыбранноеЗначение Тогда
		Ответ = Вопрос("Вы действительно хотите назначить пользователем автообмена текущего пользователя?"+Символы.ПС+"Следующий запуск обработки вызовет работу автообмена.", РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет, ,КодВозвратаДиалога.Нет);
		Если НЕ Ответ=КодВозвратаДиалога.Да Тогда
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура Выгрузить_Сервер()
	оОбъект = РеквизитФормыВЗначение("Объект");
	оОбъектыВыгрузки = РеквизитФормыВЗначение("ОбъектыВыгрузки");
	оОбъект.ВыгрузитьДанные(оОбъектыВыгрузки);
	ЗначениеВРеквизитФормы(оОбъектыВыгрузки,"ОбъектыВыгрузки");
КонецПроцедуры
&НаКлиенте
Процедура Выгрузить(Команда)
	Выгрузить_Сервер();
КонецПроцедуры

&НаСервере
Процедура Загрузить_Сервер()
	оОбъект = РеквизитФормыВЗначение("Объект");
	оОбъектыЗагрузки = РеквизитФормыВЗначение("ОбъектыЗагрузки");
	оОбъект.ЗагрузитьДанные(оОбъектыЗагрузки);
	ЗначениеВРеквизитФормы(оОбъектыЗагрузки,"ОбъектыЗагрузки");
КонецПроцедуры
&НаКлиенте
Процедура Загрузить(Команда)
	Загрузить_Сервер();
КонецПроцедуры


&НаКлиенте
Процедура Автообмен()
	Элементы.НадписьАвтообмен.Заголовок = "Автообмен включен. Последний запуск "+ТекущаяДата();
	Загрузить("Автообмен");
	Выгрузить("Автообмен");
КонецПроцедуры


&НаКлиенте
Процедура Отметки(Тбл,Знч)
	Если Знч=Неопределено Тогда
		Для Каждого стр Из Тбл Цикл
			стр.Использовать = НЕ стр.Использовать;
		КонецЦикла;
	Иначе
		Для Каждого стр Из Тбл Цикл
			стр.Использовать = Знч;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаОтметкиВсе(Команда)
	Отметки(ОбъектыВыгрузки,Истина);
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаОтметкиИзменить(Команда)
	Отметки(ОбъектыВыгрузки,Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаОтметкиСнять(Команда)
	Отметки(ОбъектыВыгрузки,Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаОтметкиВсе(Команда)
	Отметки(ОбъектыЗагрузки,Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаОтметкиИзменить(Команда)
	Отметки(ОбъектыЗагрузки,Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаОтметкиСнять(Команда)
	Отметки(ОбъектыЗагрузки,Ложь);
КонецПроцедуры


&НаКлиенте
Процедура АвтообменИспользоватьПриИзменении(Элемент)
	Если Объект.АвтообменИспользовать Тогда
		Элементы.КнопкаОбъектыВыгрузить.Доступность = Ложь;
		Элементы.КнопкаОбъектыЗагрузить.Доступность = Ложь;
		Элементы.АвтообменПериод.Доступность = Ложь;
		Элементы.ОбъектыВыгрузкиПоследнийОбмен.Видимость = Истина;
		Элементы.ОбъектыЗагрузкиПоследнийОбмен.Видимость = Истина;
		
		ПодключитьОбработчикОжидания("Автообмен", Объект.АвтообменПериод);
		Элементы.НадписьАвтообмен.Заголовок = "Автообмен включен. "+ТекущаяДата();
	Иначе
		Элементы.КнопкаОбъектыВыгрузить.Доступность = Истина;
		Элементы.КнопкаОбъектыЗагрузить.Доступность = Истина;
		Элементы.АвтообменПериод.Доступность = Истина;
		Элементы.ОбъектыВыгрузкиПоследнийОбмен.Видимость = Ложь;
		Элементы.ОбъектыЗагрузкиПоследнийОбмен.Видимость = Ложь;
		
		ОтключитьОбработчикОжидания("Автообмен");
		Элементы.НадписьАвтообмен.Заголовок = "Автообмен не включен.";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ФТППроверить_Сервер()
	оОбъект = РеквизитФормыВЗначение("Объект");
	оОбъект.ПроверкаФТП();
КонецПроцедуры
&НаКлиенте
Процедура ФТППроверить(Команда)
	ФТППроверить_Сервер();
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыЗагрузкиПериодичностьПриИзменении(Элемент)
	лПериод = Элементы.ОбъектыЗагрузки.ТекущиеДанные.Периодичность;
	Для Каждого стр Из ОбъектыЗагрузки Цикл
		стр.Периодичность = лПериод;
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура РедактироватьДопПараметрыПриИзменении(Элемент)
	Элементы.ГруппаДополнительныеПараметрыЛев.Доступность=РедактироватьДопПараметры;
	Элементы.ГруппаДополнительныеПараметрыПрав.Доступность=РедактироватьДопПараметры;
КонецПроцедуры



